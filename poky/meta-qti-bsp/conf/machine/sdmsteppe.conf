#@TYPE: Machine
#@NAME: sdmsteppe
#@DESCRIPTION: Machine configuration for QTI sdmsteppe 32bit

require  ${COREBASE}/meta-qti-bsp/conf/machine/sdm-generic32.conf

# Pull in security flags
require ${COREBASE}/meta-qti-bsp/conf/distro/include/security_flags.inc

SOC_FAMILY = "sdm670"

BASEMACHINE = "sdmsteppe"

DEFAULTTUNE = "armv7ahf-neon"

PREFERRED_PROVIDER_virtual/kernel = "linux-msm"
PREFERRED_VERSION_linux-msm = "4.14"

# Kernel to build for a different arch than default tune
TARGET_KERNEL_ARCH = "aarch64"

KERNEL_DTS_NAME = "sm6150-qrd"
KERNEL_BASE = "0x80000000"
KERNEL_TAGS_OFFSET = "0x81E00000"
KERNEL_IMAGETYPE = "Image.gz-dtb"
KERNEL_CONFIG = "${@['vendor/sdmsteppe-perf_defconfig', 'vendor/sdmsteppe_defconfig'][d.getVar('VARIANT', True) == ('' or 'debug')]}"

# Defined 64bit multilib environment to setup toolchain to compile 64bit kernel
require conf/multilib.conf
MULTILIBS = "multilib:lib64"
DEFAULTTUNE_virtclass-multilib-lib64 = "aarch64"
TARGET_VENDOR_virtclass-multilib-lib64="-oe"

# Enable selinux
DISTRO_FEATURES_append = " selinux"
DEFAULT_ENFORCING = "enforcing"

# enable audio as a loadable kernel module
DISTRO_FEATURES_append = " audio-dlkm"

# readonly-rootfs
DISTRO_FEATURES_append = " ro-rootfs"

# Support A/B partition scheme
DISTRO_FEATURES += "ab-boot-support"

#Disable journalling for RO rootfs. Set the block size to 4096.
IMAGE_EXT4_EXTRA_OPTIONS = "${@bb.utils.contains('DISTRO_FEATURES','ro-rootfs',' -J -b 4096 ','',d)}"

# Flag to Enable QPermissions
QPERMISSIONS_ENABLE = "1"

QPERM_FILE_pn-start-script-ab-slot-util = "${bindir}/getslotsuffix:root:system"
QPERM_FILE_pn-system-core = "${sysconfdir}/build.prop:leprop:leprop ${sysconfdir}/initscripts/logd:logd:logd"
QPERM_FILE_pn-system-prop = "/build.prop:leprop:leprop"


# Userspace needs below given macro to support libion after kernel 4.12.
TARGET_CFLAGS += " -DTARGET_ION_ABI_VERSION=2"
TARGET_CXXFLAGS += " -DTARGET_ION_ABI_VERSION=2"

PREFERRED_PROVIDER_virtual/libgles1 = "adreno"
PREFERRED_PROVIDER_virtual/libgles2 = "adreno"
PREFERRED_PROVIDER_virtual/egl      = "adreno"
